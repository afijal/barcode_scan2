plugins {
    id "com.android.library"
    id "kotlin-android"
    id "com.google.protobuf" version "0.9.4"
    id "dev.flutter.flutter-gradle-plugin"
}

group 'de.mintware.barcode_scan'
version '1.0-SNAPSHOT'

allprojects {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

android {
    namespace 'de.mintware.barcode_scan'

    if (project.android.hasProperty('namespace')) {
        namespace('de.mintware.barcode_scan')
    }

    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.proto.srcDirs += '../protos'
    }
    defaultConfig {
        minSdkVersion 19
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "proguard-rules.pro"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}


protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:4.33.0'
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.google.protobuf:protobuf-javalite:4.33.0'
    api 'com.google.android.material:material:1.13.0'
}
